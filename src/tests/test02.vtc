varnishtest "awsrest"

server s1 {
	rxreq
	expect req.http.x-req == "1"
	expect req.http.x-req-security-token == "req-token"
	expect req.http.x-bereq == "1"
	expect req.http.x-bereq-security-token == "bereq-token"
	txresp
} -start

varnish v1 -vcl+backend {
	import ${vmod_awsrest};

	sub vcl_recv {
		awsrest.v4_generic(
			service           = "s3",
			region            = "ap-northeast-1",
			access_key        = "[Your Access Key]",
			secret_key        = "[Your Secret Key]",
			token             = "req-token",
			signed_headers    = "host;",
			canonical_headers = "host:" + req.http.host + awsrest.lf()
		);
		if(req.http.Authorization){
			set req.http.x-req = "1";
		}
		if(req.http.x-amz-security-token){
			set req.http.x-req-security-token = req.http.x-amz-security-token;
		}
	}
	sub vcl_backend_fetch{
		unset bereq.http.Authorization;
		awsrest.v4_generic(
			service           = "s3",
			region            = "ap-northeast-1",
			access_key        = "[Your Access Key]",
			secret_key        = "[Your Secret Key]",
			token             = "bereq-token",
			signed_headers    = "host;",
			canonical_headers = "host:" + bereq.http.host + awsrest.lf()
		);
		if(bereq.http.Authorization){
			set bereq.http.x-bereq = "1";
		}
		if(bereq.http.x-amz-security-token){
			set bereq.http.x-bereq-security-token = bereq.http.x-amz-security-token;
		}
	}
} -start

client c1 {
	txreq -url "/"
	rxresp
}

client c1 -run
